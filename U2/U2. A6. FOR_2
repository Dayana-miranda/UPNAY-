Una zapatera necesita registrar los datos de sus vendedores y los datos de las ventas individuales para calcular algunos estándares de calidad a fin de mejorar la productividad y otorgar bonos, así como "cortar cabezas". 

El sistema debe solicitar el a cuántos vendedores se va a registrar su información y de cada vendedor debe solicitar cuántas ventas realizó y de cuánto fue el valor de cada una. 

Después de terminar el registro de las ventas, debe mostrar el total de las ventas de ese vendedor, el número de ventas que hizo y su venta más alta. Si la venta más alta es menor a $100 o el total es menor de $1,500, debe mostrar un mensaje de que pase a Recursos Humanos. 

El reporte final debe entregar un reporte de cuántos vendedores se registraron en el sistema y cuánto fue el total de las ventas globales de la empresa . Si el reporte global es mayor a $20,000 deberá mostrar un mensaje de que se alcanzó la meta , de lo contrario mostrará un mensaje que habrá que convocar una reunión . 

Algoritmo RegistroVentas
	Definir cantidadVendedores, cantidadTransacciones, totalVentasPorVendedor, mayorVenta, contadorTransacciones, montoVenta, idVendedor, totalVentasEmpresa, acumuladoGlobal Como Real
    Definir acumuladoVendedor, acumuladoEmpresa Como Real
    
    acumuladoVendedor = 0
    acumuladoEmpresa = 0
    
    Escribir "¿Cuántos vendedores quieres registrar?"
    Leer cantidadVendedores
    
    Para idVendedor = 1 Hasta cantidadVendedores Hacer
        Escribir "Ingrese el número de ventas realizadas por el vendedor ", idVendedor
        Leer cantidadTransacciones
        totalVentasPorVendedor = 0
        mayorVenta = 0
        
        Para contadorTransacciones = 1 Hasta cantidadTransacciones Hacer
            Escribir "Ingrese el valor de la venta ", contadorTransacciones
            Leer montoVenta
            totalVentasPorVendedor = totalVentasPorVendedor + montoVenta
            Si montoVenta > mayorVenta Entonces
                mayorVenta = montoVenta
            FinSi
        FinPara
        
        Escribir "Total de ventas del vendedor ", idVendedor, ": ", totalVentasPorVendedor
        Escribir "Número de ventas realizadas: ", cantidadTransacciones
        Escribir "Venta más alta: ", mayorVenta
        
        Si mayorVenta < 100 O totalVentasPorVendedor < 1500 Entonces
            Escribir "Pase a Recursos Humanos"
        FinSi
        
        acumuladoVendedor <- acumuladoVendedor + totalVentasPorVendedor
        acumuladoEmpresa <- acumuladoEmpresa + totalVentasPorVendedor
    FinPara
    
    Escribir "Total de vendedores registrados: ", cantidadVendedores
    Escribir "Total de ventas globales de la empresa: ", acumuladoEmpresa
    
    Si acumuladoEmpresa > 20000 Entonces
        Escribir "Alcanzamos la meta siuuuuuuuuuuuuuuu"
    Sino
        Escribir "Habrá que convocar una reunión."
    FinSi
FinAlgoritmo
